{-# LANGUAGE MultiWayIf #-}
{-# LANGUAGE Unsafe     #-}
{-|
Module      : Text.Pandoc.Filter.Pyplot
Description : Pandoc filter to create Matplotlib figures from code blocks
Copyright   : (c) Laurent P RenÃ© de Cotret, 2019
License     : MIT
Maintainer  : laurent.decotret@outlook.com
Stability   : stable
Portability : portable

This module defines a Pandoc filter @makePlot@ that can be
used to walk over a Pandoc document and generate figures from
Python code blocks.

The syntax for code blocks is simple, Code blocks with the @plot_target=...@
attribute will trigger the filter. The code block will be reworked into a Python
script and the output figure will be captured.

To trigger pandoc-pyplot, one of the following is __required__:

    * @.pyplot@: Trigger pandoc-pyplot but let it decide on a filename
    * @plot_target=...@: Filepath where the resulting figure should be saved.

Here are the possible attributes what pandoc-pyplot understands:

    * @plot_caption="..."@: Specify a plot caption (or alternate text).
    * @dpi=...@: Specify a value for figure resolution, or dots-per-inch. Default is 80DPI.
    * @plot_include=...@: Path to a Python script to include before the code block. Ideal to avoid repetition over many figures.

Here are some example blocks in Markdown:

@
This is a paragraph

```{plot_target=my_figure.jpg plot_caption="This is a caption."}
import matplotlib.pyplot as plt

plt.figure()
plt.plot([0,1,2,3,4], [1,2,3,4,5])
plt.title('This is an example figure')
```
@
-}
module Text.Pandoc.Filter.Pyplot (
        makePlot
      , plotTransform
      , PandocPyplotError(..)
      -- For testing purposes only
      , makePlot'
      , targetKey
      , directoryKey
      , captionKey
      , dpiKey
      , includePathKey
    ) where

import           Control.Monad                ((>=>), replicateM)
import           Data.Char                    (chr)
import qualified Data.Map.Strict              as Map
import           Data.Maybe                   (fromMaybe)
import           Data.Monoid                  ((<>))
import           Data.Version                 (showVersion)
import           Paths_pandoc_pyplot          (version)

import           System.Directory             (doesDirectoryExist)
import           System.FilePath              (isValid, replaceExtension,
                                               takeDirectory, (</>), makeValid)
import           System.Random                (getStdRandom, randomR)

import           Text.Pandoc.Definition
import           Text.Pandoc.Walk             (walkM)

import           Text.Pandoc.Filter.Scripting

-- | Possible errors returned by the filter
data PandocPyplotError = ScriptError Int                -- ^ Running Python script has yielded an error
                       | InvalidTargetError FilePath    -- ^ Invalid figure path
                       | MissingDirectoryError FilePath -- ^ Directory where to save figure does not exist
                       | BlockingCallError              -- ^ Python script contains a block call to 'show()'
        deriving Eq

instance Show PandocPyplotError where
    -- | Translate filter error to an error message
    show (ScriptError exitcode)          = "Script error: plot could not be generated. Exit code " <> (show exitcode)
    show (InvalidTargetError fname)      = "Target filename " <> fname <> " is not valid."
    show (MissingDirectoryError dirname) = "Target directory " <> dirname <> " does not exist."
    show BlockingCallError               = "Script contains a blocking call to show, like 'plt.show()'"

-- | Generate a random filepath of a certain length
-- Idea
randomFigureFilePath :: Int -> IO FilePath
randomFigureFilePath len = (++ ".png") <$> replicateM len randomAlphaChar
    where
        -- lowercase a - z only
        randomAlphaChar = getStdRandom $ randomR (chr 97,chr 122)

-- | Datatype containing all parameters required
-- to run pandoc-pyplot
data FigureSpec = FigureSpec
    { path        :: FilePath        -- ^ Fully-rendered save path
    , caption     :: String          -- ^ Alternate text for the figure (optional).
    , script      :: PythonScript    -- ^ Source code for the figure.
    , dpi         :: Int             -- ^ Dots-per-inch of figure
    , includePath :: Maybe FilePath  -- ^ Path to a Python to be included before the script.
    , blockAttrs  :: Attr            -- ^ Attributes not related to @pandoc-pyplot@ will be propagated.
    }

-- | Use figure specification to render a full plot script, including everything except plot capture
renderScript :: FigureSpec -> IO PythonScript
renderScript spec = do
    includeScript <- fromMaybe (return "") $ readFile <$> (includePath spec)
    return $ mconcat [ "# Source code for ", path spec, "\n"
                     , "# Generated by pandoc-pyplot ", showVersion version, "\n\n"
                     , includeScript, "\n", script spec]

-- | Keys that pandoc-pyplot will look for in code blocks. 
targetKey, directoryKey, captionKey, dpiKey, includePathKey :: String
targetKey      = "target"
directoryKey   = "directory"
captionKey     = "caption"
dpiKey         = "dpi"
includePathKey = "include"

-- | list of all keys related to pandoc-pyplot. These are only exported for testing purposes.
inclusionKeys :: [String]
inclusionKeys = [targetKey, directoryKey, captionKey, dpiKey, includePathKey]

-- | Determine inclusion specifications from Block attributes.
-- Note that the target key is required, but all other parameters are optional
parseFigureSpec :: Block -> Maybe (IO FigureSpec)
parseFigureSpec (CodeBlock (id', cls, attrs) content) 
    | "pyplot" `elem` cls = Just $ createInclusion $ Map.lookup targetKey attrs'
    | otherwise           = Nothing
    where
        attrs' = Map.fromList attrs
        filteredAttrs = filter (\(k,_) -> k `notElem` inclusionKeys) attrs

        createInclusion :: Maybe FilePath -> IO FigureSpec
        createInclusion (Just fp) = return $ createInclusionWithExplicitTarget fp
        createInclusion  Nothing  = do
            let dir = Map.findWithDefault "." directoryKey attrs'
            createInclusionWithExplicitTarget . (dir </>) <$> (randomFigureFilePath 8)

        createInclusionWithExplicitTarget :: FilePath -> FigureSpec
        createInclusionWithExplicitTarget fname = FigureSpec { 
              path       = makeValid fname
            , caption    = Map.findWithDefault "Figure generated by pandoc-pyplot" captionKey attrs'
            , script     = content
            , dpi        = read $ Map.findWithDefault "80" dpiKey attrs' -- TODO: clunky
            , includePath = Map.lookup includePathKey attrs'
            -- Propagate attributes that are not related to pandoc-pyplot
            , blockAttrs = (id', mempty, filteredAttrs)
            }
    
parseFigureSpec _ = Nothing

-- | Main routine to include Matplotlib plots.
-- Code blocks containing the attributes @plot_target@ are considered
-- Python plotting scripts. All other possible blocks are ignored.
-- The source code is also saved in another file, which can be access by
-- clicking the image
makePlot' :: Block -> IO (Either PandocPyplotError Block)
makePlot' block = 
    case parseFigureSpec block of
        -- Could not parse - leave code block unchanged
        Nothing -> return $ Right block
        -- Could parse : run the script and capture output
        Just spec' -> do
            
            -- Extract specifications from IO
            -- This is a type travesty, I'm sorry
            spec <- spec'

            -- Rendered script, including possible inclusions and other additions
            -- except the plot capture.
            rendered <- renderScript spec

            let figurePath = path spec
                figureDir = takeDirectory figurePath

            -- Check that the directory in which to save the figure exists
            validDirectory <- doesDirectoryExist $ takeDirectory figurePath

            if | not (isValid figurePath)         -> return $ Left $ InvalidTargetError figurePath
               | not validDirectory               -> return $ Left $ MissingDirectoryError figureDir
               | hasBlockingShowCall rendered     -> return $ Left $ BlockingCallError
               | otherwise -> do

                -- Running the script
                -- A plot capture (plt.savefig(...)) is added as well
                result <- runTempPythonScript $ addPlotCapture (path spec) rendered

                case result of
                    ScriptFailure code -> return $ Left $ ScriptError code
                    ScriptSuccess -> do
                        -- Save the original script into a separate file
                        -- so it can be inspected
                        -- Note : using a .txt file allows to view source directly
                        --        in the browser, in the case of HTML output
                        let sourcePath = replaceExtension figurePath ".txt"
                        writeFile sourcePath rendered

                        -- Propagate attributes that are not related to pandoc-pyplot
                        let relevantAttrs = blockAttrs spec
                            srcTarget = Link nullAttr [Str "Source code"] (sourcePath, "")
                            caption'   = [Str $ caption spec, Space, Str "(", srcTarget, Str ")"]
                            -- To render images as figures with captions, the target title
                            -- must be "fig:"
                            -- Janky? yes
                            image     = Image relevantAttrs caption' (figurePath, "fig:")

                        return $ Right $ Para $ [image]

-- | Highest-level function that can be walked over a Pandoc tree.
-- All code blocks that have the 'plot_target' parameter will be considered
-- figures.
makePlot :: Block -> IO Block
makePlot = makePlot' >=> either (fail . show) return

-- | Walk over an entire Pandoc document, changing appropriate code blocks
-- into figures.
plotTransform :: Pandoc -> IO Pandoc
plotTransform = walkM makePlot
